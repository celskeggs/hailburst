import os

import SCons.Defaults
import SCons.Tool

Import('env')

# host program
GDB_BINUTILS = "#/../../binutils-gdb"

excise_source = [
    "excise.c",
    GDB_BINUTILS + "/bfd/libbfd.a",
    GDB_BINUTILS + "/libiberty/libiberty.a",
]

if not os.getenv("SCONS_VERBOSE"):
    env["OBJCOMSTR"] = "[${PLATFORM} - COMPILE] ${SOURCE}"
    env["LINKCOMSTR"] = "[${PLATFORM} -    LINK] ${TARGET}"
    env["PPCOMSTR"] = "[${PLATFORM} - PREPROC] ${SOURCE}"
    env["PPMODCOMSTR"] = "[${PLATFORM} -  MACROS] ${SOURCE}"
    env["ARCOMSTR"] = "[${PLATFORM} - ARCHIVE] ${TARGET}"
    env["RANLIBCOMSTR"] = "[${PLATFORM} -  RANLIB] ${TARGET}"
    env["ASCOMSTR"] = "[${PLATFORM} - ASSEMBL] ${SOURCE}"
    env["STRIPCOMSTR"] = "[${PLATFORM} -   STRIP] ${SOURCE}"
    env["BIN2OBJCOMSTR"] = "[${PLATFORM} - BIN2OBJ] ${SOURCE}"
    env["OBJ2BINCOMSTR"] = "[${PLATFORM} - OBJ2BIN] ${SOURCE}"


siren_deps = Glob("siren/*.py") + Glob("siren/*/*.py")
env["PYTHONPATH"] = Dir('#/toolchain')
env["PPSUFFIX"] = ".iraw"
env["RWSUFFIX"] = ".i"
env["PPCOM"] = '$CC -o $TARGET -E $CFLAGS $CCFLAGS -D__PYTHON_PREPROCESS__ $_CCCOMCOM $SOURCES'
env["PYTHONPPFLAGS"] = []
if os.getenv("PP_RAWLINES"):
    env["PYTHONPPFLAGS"] += ["--rawlines"]
env["PPMODCOM"] = 'env PYTHONPATH=$PYTHONPATH python3 -m siren $PYTHONPPFLAGS $SOURCES $TARGET'
# eliminate $_CCCOMCOM here because it's used for preprocessor flags that are not needed here
env["OBJCOM"] = '$CC -o $TARGET -c $CFLAGS $CCFLAGS $SOURCES'

env['BUILDERS']['PP'] = Builder(
    action=Action("$PPCOM", "$PPCOMSTR"),
    suffix='$PPSUFFIX',
    src_suffix='.c',
    source_scanner=SourceFileScanner,
    single_source=1,
)


def plugin_dep_emitter(target, source, env):
    env.Depends(target, siren_deps)
    return target, source


env['BUILDERS']['PPMOD'] = Builder(
    action=Action("$PPMODCOM", "$PPMODCOMSTR"),
    emitter=plugin_dep_emitter,
    suffix='$RWSUFFIX',
    src_suffix='$PPSUFFIX',
    src_builder='PP',
    single_source=1,
)
ObjAction = Action("$OBJCOM", "$OBJCOMSTR")
ASAction = Action("$ASCOM", "$ASCOMSTR")
CAction = Action("$CCCOM", "$CCCOMSTR")
static_object_builder = Builder(
    action={'$RWSUFFIX': ObjAction, '.s': ASAction, '.S': CAction},
    emitter=SCons.Defaults.StaticObjectEmitter,
    prefix='$OBJPREFIX',
    suffix='$OBJSUFFIX',
    src_suffix=['$RWSUFFIX', '.s', '.S'],
    src_builder='PPMOD',
    single_source=1,
)
env['BUILDERS']['Object'] = static_object_builder
env['BUILDERS']['StaticObject'] = static_object_builder

host_env = Environment(CCCOMSTR="[ HOST - COMPILE] ${SOURCE}", LINKCOMSTR="[ HOST -    LINK] ${TARGET}")

env["REPLICA_LINK_PY"] = File('linker.py')
env["EXCISE"] = host_env.Program("excise", excise_source, CPPPATH=[GDB_BINUTILS + "/bfd"], LIBS=["dl", "z"])
# TODO: can this be a list?
env["REPLICA_LINK"] = (
        "python3 $REPLICA_LINK_PY " +
        "--archiver $AR --excise $EXCISE --objcopy $OBJCOPY --replica-script $REPLICA_SCRIPT " +
        "--linker " + env["LINK"]  # make a copy of $LINK so that it can be replaced later
)
